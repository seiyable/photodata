//============= notes =============
/*
This is a code for displaying the result of color analysis
of a photo using Imagga Color API.
You need to prepare a csv file that is generated by "colorExtraction.pde".
*/

//============= global variables =============
Table table;
PImage img;
String filepath = "http://s5884.com/w/photodata_final_photos/";
String filename = "japan_37.jpg";

ArrayList<ExtractedColor> exColors = new ArrayList<ExtractedColor>();

//============= setup() =============
void setup(){
  size(1000, 600); //set window size
  
  //load a CSV file
  table = loadTable("../colorExtraction/data/photodata.csv", "header");
  //load an image file
  img = loadImage("../photodata_final_photos/" + filename);
  
  //check all the rows in the CSV file --------------------
  int bg_counter = 0, fg_counter = 0, ig_counter = 0;
  for (TableRow row : table.rows()) {
    String fileurl = row.getString("fileurl");
    
    //if the row has the filename you want to check
    if(fileurl.equals(filepath + filename)){
      String kind = row.getString("kind");
      
      //get RGB values
      int r = row.getInt("r");
      int g = row.getInt("g");
      int b = row.getInt("b");
      color colour = color(r,g,b); //integrate the values
      
      //get percentage
      int percentage = int(row.getFloat("percent"));
      
      //add those values to arrayList
      int counter = 0;
      if(kind.equals("background")) counter = bg_counter;
      else if(kind.equals("foreground")) counter = fg_counter;
      else if(kind.equals("image")) counter = ig_counter;
      exColors.add(new ExtractedColor(kind, colour, percentage, counter));
      println(kind + " " + counter + " hex: " + colour + ", percentage: " + percentage);
      
      //increment counter
      if(kind.equals("background")) bg_counter++;
      else if(kind.equals("foreground")) fg_counter++;
      else if(kind.equals("image")) ig_counter++;
    }
  }  
}

//============= draw() =============
void draw(){
  //set image
  pushMatrix();
  scale(height/float(img.height));
  image(img, 0, 0);
  popMatrix();
  
  //set texts
  text("bg", width-90, 10);
  text("fg", width-60, 10);
  text("ig", width-30, 10);
  
  //display rectangles with extracted colors
  int rectX = width - 100;
  int rectY = 15;
  for(ExtractedColor e : exColors){
    int delta = 0;
    if(e.kind.equals("background")) delta = 0;
    else if(e.kind.equals("foreground")) delta = 30;
    else if(e.kind.equals("image")) delta = 60;
    
    fill(e.colour);
    rect(rectX + delta, rectY + e.counter*20, 30, 20);
    fill(255);
    text(str(e.percentage), rectX + delta + 10, rectY + e.counter*20 + 15);
  }
}
